qtPrepareTool(QMAKE_QRESTBUILDER, qrestbuilder)

isEmpty(QRESTBUILDER_DIR):QRESTBUILDER_DIR = .
isEmpty(MOC_DIR):MOC_DIR = .

debug_and_release {
	CONFIG(debug, debug|release):SUFFIX = /debug
	CONFIG(release, debug|release):SUFFIX = /release
}

QRESTBUILDER_DIR = $$QRESTBUILDER_DIR$$SUFFIX

{
QRB_DEPRECATED_ECHO="echo ${QMAKE_FILE_IN}': Using REST_API_CLASSES/REST_API_OBJECTS has been deprecated! Please convert your definitions to XML using qrestbuilder --convert <type> --in <json> --impl <xml> and add the newly generated definitions to your project as REST_API_FILES' 1>&2"

qrestbuilder_cc.name = QRESTBUILDER class ${QMAKE_FILE_IN}.h
qrestbuilder_cc.input = REST_API_CLASSES
qrestbuilder_cc.variable_out = REST_API_FILES
qrestbuilder_cc.commands = $$QRB_DEPRECATED_ECHO && $$QMAKE_QRESTBUILDER --convert class --in ${QMAKE_FILE_IN} --impl ${QMAKE_FILE_OUT}
qrestbuilder_cc.output = $$QRESTBUILDER_DIR/${QMAKE_FILE_BASE}.xml
qrestbuilder_cc.CONFIG += target_predeps
qrestbuilder_cc.depends += $$QMAKE_QRESTBUILDER_EXE
QMAKE_EXTRA_COMPILERS += qrestbuilder_cc

qrestbuilder_co.name = QRESTBUILDER object ${QMAKE_FILE_IN}.h
qrestbuilder_co.input = REST_API_OBJECTS
qrestbuilder_co.variable_out = REST_API_FILES
qrestbuilder_co.commands = $$QRB_DEPRECATED_ECHO && $$QMAKE_QRESTBUILDER --convert object --in ${QMAKE_FILE_IN} --impl ${QMAKE_FILE_OUT}
qrestbuilder_co.output = $$QRESTBUILDER_DIR/${QMAKE_FILE_BASE}.xml
qrestbuilder_co.CONFIG += target_predeps
qrestbuilder_co.depends += $$QMAKE_QRESTBUILDER_EXE
QMAKE_EXTRA_COMPILERS += qrestbuilder_co

qrestbuilder_c.name = QRESTBUILDER ${QMAKE_FILE_IN}.h
qrestbuilder_c.input = REST_API_FILES
qrestbuilder_c.variable_out = QRESTBUILDER_HEADERS
qrestbuilder_c.commands = $$QMAKE_QRESTBUILDER --in ${QMAKE_FILE_IN} --header ${QMAKE_FILE_OUT} --impl $$QRESTBUILDER_DIR/${QMAKE_FILE_BASE}$${first(QMAKE_EXT_CPP)}
qrestbuilder_c.output = $$QRESTBUILDER_DIR/${QMAKE_FILE_BASE}$${first(QMAKE_EXT_H)}
qrestbuilder_c.CONFIG += target_predeps
qrestbuilder_c.depends += $$QMAKE_QRESTBUILDER_EXE
QMAKE_EXTRA_COMPILERS += qrestbuilder_c

qrestbuilder_m.name = QRESTBUILDER_MOC ${QMAKE_FILE_IN}
qrestbuilder_m.input = QRESTBUILDER_HEADERS
qrestbuilder_m.variable_out = GENERATED_SOURCES
qrestbuilder_m.commands = ${QMAKE_FUNC_mocCmdBase} ${QMAKE_FILE_IN} -o ${QMAKE_FILE_OUT}
qrestbuilder_m.output = $$MOC_DIR/$${QMAKE_H_MOD_MOC}${QMAKE_FILE_BASE}$${first(QMAKE_EXT_CPP)}
qrestbuilder_m.CONFIG += target_predeps
qrestbuilder_m.depends += $$QMAKE_QRESTBUILDER_EXE $$WIN_INCLUDETEMP $$moc_predefs.output
qrestbuilder_m.dependency_type = TYPE_C
QMAKE_EXTRA_COMPILERS += qrestbuilder_m

qrestbuilder_s.name = QRESTBUILDER ${QMAKE_FILE_IN}.cpp
qrestbuilder_s.input = QRESTBUILDER_HEADERS
qrestbuilder_s.variable_out = GENERATED_SOURCES
qrestbuilder_s.commands = $$escape_expand(\\n) # force creation of rule
qrestbuilder_s.output = $$QRESTBUILDER_DIR/${QMAKE_FILE_BASE}$${first(QMAKE_EXT_CPP)}
qrestbuilder_s.CONFIG += target_predeps
QMAKE_EXTRA_COMPILERS += qrestbuilder_s
}

INCLUDEPATH += $$absolute_path($$QRESTBUILDER_DIR, $$OUT_PWD)
QMAKE_DIR_REPLACE += QRESTBUILDER_DIR
QMAKE_DIR_REPLACE_SANE += QRESTBUILDER_DIR
